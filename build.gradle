buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.4.2.RELEASE")
        classpath "nu.studer:gradle-jooq-plugin:2.0.2"
    }
}
plugins {
    id "com.moowork.gulp" version "1.0.1"
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'findbugs'
apply plugin: 'idea'
apply plugin: 'nu.studer.jooq'

gulp {
    workDir = file("${project.projectDir}")
    colors = true
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

configurations {
    all*.exclude group: 'commons-logging'
}

jar {
    baseName = 'fpvlaptracker'
    version =  '0.4'
}

sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'


tasks.withType(FindBugs) {
    ignoreFailures = true 
    reports {
        xml.enabled = false
        html.enabled = true
    }
    classes = classes.filter {
        !it.path.contains("de/warhog/fpvlaptracker/jooq")
    }    
}

if (!hasProperty('mainClass')) {
    ext.mainClass = 'de.warhog.fpvlaptracker.Laptracker'
}

compileJava.dependsOn gulp_build

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '1.4.2.RELEASE'
    compile group: 'org.springframework', name: 'spring-websocket', version: '4.3.5.RELEASE'
    compile group: 'org.springframework', name: 'spring-messaging', version: '4.3.5.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: '1.4.3.RELEASE'
    compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    compile group: 'com.google.code.findbugs', name: 'annotations', version: '3.0.1u2'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.8'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.8.5'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.5'
    compile group: 'org.jooq', name: 'jooq', version: '3.9.0'
    compile group: 'com.h2database', name: 'h2', version: '1.4.193'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
    jooqRuntime 'com.h2database:h2:1.4.193'
}


jooq {
    version = '3.9.0'
    edition = 'OSS'
    flt(sourceSets.main) {
        jdbc {
            driver = 'org.h2.Driver'
            url = 'jdbc:h2:./flt.h2'
            user = 'sa'
            password = ''
            schema = 'public'
        }
        generator {
            name = 'org.jooq.util.DefaultGenerator'
            strategy {
                name = 'org.jooq.util.DefaultGeneratorStrategy'
            }
            database {
                name = 'org.jooq.util.h2.H2Database'
                inputSchema = 'PUBLIC'
            }
            generate {
                relations = true
                deprecated = false
                records = true
                immutablePojos = true
                fluentSetters = true
            }
            target {
                packageName = 'de.warhog.fpvlaptracker.jooq'
                directory = "src/main/java"
            }
        }
    }
}

project.tasks.getByName('compileJava').dependsOn -= 'generateFltJooqSchemaSource'
